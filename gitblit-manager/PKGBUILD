# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=gitblit-manager
_pkgname=manager
pkgver=0.9.3
pkgrel=1
pkgdesc="Remote administration client for Gitblit."
arch=(any)
url="http://gitblit.com"
license=(APACHE)
depends=('java-runtime>=6' 'bash')
install=${pkgname}.install
changelog=Changelog
source=("http://gitblit.googlecode.com/files/manager-${pkgver}.zip")
md5sums=('1fb683fc3d02ea094e6f03cf73aa86f5')

build() {
  msg2 "Generate executable script..."
  cat > "${srcdir}/${pkgname}.sh" << EOF
#!/bin/sh
cd /tmp # FIXME Gitblit manager downloads stuff to current working directory, so this is the least uggly solution...
"\$JAVA_HOME/bin/java" -jar '/usr/share/java/${pkgname}/${pkgname}.jar' "\$@"
EOF

  msg2 "Generate desktop application entry..."
  cat > "${srcdir}/${pkgname}.desktop" << EOF
[Desktop Entry]
Name=Gitblit manager
Comment=${pkgdesc}
TryExec=${pkgname}
Exec=${pkgname} %u
Icon=java
Terminal=false
Type=Application
Categories=Application;Utility;
EOF
}

package() {
  msg2 "Install Java livraries in /usr/share/java/${pkgname}..."
  install -Dm755 "${srcdir}/${_pkgname}-${pkgver}.jar" "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"

  msg2 "Install an executable at /usr/bin/${pkgname}..."
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  msg2 "Install a desktop application entry at /usr/share/applications..."
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  msg2 "Install documentation resources in /usr/share/doc/${pkgname}..."
  install -Dm644 "${srcdir}/NOTICE" "${pkgdir}/usr/share/doc/${pkgname}/NOTICE"

  msg2 "Install copyright resources in /usr/share/licenses/${pkgname}..."
  install -Dm644 "${srcdir}/LICENSE"     "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  ln -s /usr/share/doc/${pkgname}/NOTICE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
