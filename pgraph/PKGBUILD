# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=pgraph
pkgver=2.3
pkgrel=2
pkgdesc="Read multiple performance data formats and produce graphs either interactively or in batch mode."
arch=('any')
url="http://www.ibm.com/developerworks/wikis/display/WikiPtype/pGraph"
license=('unknown') # FIXME
depends=('java-runtime>=5' 'bash')
changelog=Changelog
source=(http://www.ibm.com/developerworks/wikis/download/attachments/53871809/pGraph.jar?version=5
        http://www.ibm.com/developerworks/wikis/download/attachments/53871809/pGraph.properties?version=1
        ${pkgname}.desktop)
noextract=(pGraph.jar)
md5sums=('a22000cb33e76a1652467a414ada1e5c'
         '6bbb3d137aa3c45ff1c96410dac800da'
         '97fb05f1666a2efe703abf32aa35c14b')

package() {

  msg2 "Install the library at /usr/share/java/${pkgname}..."
  install -Dm755 "${srcdir}/pGraph.jar?version=5" "${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar"

  msg2 "Install the default settings file at /etc/${pkgname}..."
  install -Dm644 "${srcdir}/pGraph.properties?version=1" "${pkgdir}/usr/share/java/${pkgname}/pGraph.properties"
  install -dm755 "${pkgdir}/etc"
  ln -s "/usr/share/java/${pkgname}/pGraph.properties" "${pkgdir}/etc/${pkgname}.properties"

  msg2 "Install an executable at /usr/bin/${pkgname}..."
  install -dm755 ${pkgdir}/usr/bin
  cat <<EOF > ${pkgdir}/usr/bin/${pkgname}
#!/bin/sh
cd '/usr/share/java/${pkgname}'
"\$JAVA_HOME/bin/java" -jar '${pkgname}.jar' "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  msg2 "Install a desktop application entry at /usr/share/applications..."
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

}

# vim:set ts=2 sw=2 et:
