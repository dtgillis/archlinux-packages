# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=gatling-stress-tool # Name collision with https://aur.archlinux.org/packages.php?ID=7159
_pkgname=gatling
pkgver=1.2.3
pkgrel=1
pkgdesc="An optimized stress test tool."
arch=(any)
url="http://gatling-tool.org"
license=(APACHE)
depends=('java-environment>=6' 'bash' 'desktop-file-utils')
install=${_pkgname}.install
changelog=Changelog
backup=(opt/${_pkgname}/conf/gatling.conf
        opt/${_pkgname}/conf/logback.xml)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/downloads/excilys/${_pkgname}/${_pkgname}-charts-highcharts-${pkgver}-bundle.tar.gz")
md5sums=('caf6744617346e7ab19b3e50cf67ded4')

build() {
  msg2 "Generate scripts for /etc/profile.d..."
  cat <<EOF > "${srcdir}"/${_pkgname}.profile.d.sh
export GATLING_HOME=/opt/${_pkgname}
EOF
  cat <<EOF > "${srcdir}"/${_pkgname}.profile.d.csh
setenv GATLING_HOME "/opt/${_pkgname}"
EOF

  msg2 "Generate desktop application entry for recorder..."
  cat > "${srcdir}/recorder.desktop" << EOF
[Desktop Entry]
Name=Gatling recorder
Comment=${pkgdesc}
TryExec=${_pkgname}-recorder
Exec=${_pkgname}-recorder %u
Icon=/opt/gatling/assets/style/logo.png
Terminal=false
Type=Application
Categories=Application;Utility;
EOF
}

package() {
  msg2 "Install the assembly at /opt/${_pkgname}..."
  install -dm755                                              "${pkgdir}/opt/${_pkgname}"
  cp -a "${srcdir}"/${_pkgname}-charts-highcharts-${pkgver}/* "${pkgdir}/opt/${_pkgname}"

  msg2 "Set up the GATLING_HOME environment variable in /etc/profile.d..."
  install -Dm755 "${srcdir}/${_pkgname}.profile.d.sh"  "${pkgdir}/etc/profile.d/${_pkgname}.sh"
  install -Dm755 "${srcdir}/${_pkgname}.profile.d.csh" "${pkgdir}/etc/profile.d/${_pkgname}.csh"

  msg2 "Install links to documentation resources at /usr/share/doc/${_pkgname}..."
  install -dm755                    "${pkgdir}/usr/share/doc/${_pkgname}"
  ln -s /opt/${_pkgname}/user-files "${pkgdir}/usr/share/doc/${_pkgname}/"

  msg2 "Install links to configuration files at /etc/${_pkgname}..."
  install -dm755                               "${pkgdir}/etc/${_pkgname}"
  ln -s /opt/${_pkgname}/conf/${_pkgname}.conf "${pkgdir}/etc/${_pkgname}/${_pkgname}.conf"
  ln -s /opt/${_pkgname}/conf/logback.xml      "${pkgdir}/etc/${_pkgname}/logback.xml"

  msg2 "Install links to executables in /usr/bin..."
  install -dm755                            "${pkgdir}/usr/bin"
  ln -s /opt/${_pkgname}/bin/${_pkgname}.sh "${pkgdir}/usr/bin/${_pkgname}"
  ln -s /opt/${_pkgname}/bin/recorder.sh    "${pkgdir}/usr/bin/${_pkgname}-recorder"

  msg2 "Install a desktop application entry for the recorder at /usr/share/applications..."
  install -Dm644 "${srcdir}/recorder.desktop" "${pkgdir}/usr/share/applications/${_pkgname} recorder.desktop"
}

# vim:set ts=2 sw=2 et:
