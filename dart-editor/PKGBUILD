# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=dart-editor
pkgver=0.1.0.1141
pkgrel=1
pkgdesc="Editor for the Dart programming language."
arch=('i686' 'x86_64')
url="http://www.dartlang.org/docs/getting-started/editor/index-linux.html"
license=('EPL')
depends=('java-runtime' 'gtk2')
[[ ${CARCH} = i686 ]] && _arch=x86 && md5sums=('f564e2991ce34554ab322db538008e03')
[[ ${CARCH} = x86_64 ]] && _arch=x86_64 && md5sums=('6116e3ae8571827dad97cfaab4286224')
source=("http://gsdview.appspot.com/dart-editor-archive-integration/1141/DartBuild-linux.gtk.${_arch}.zip")

build() {

  msg2 "Generate desktop application entry..."
  cat > "${srcdir}"/${pkgname}.desktop << EOF
[Desktop Entry]
Version=${pkgver}
Encoding=UTF-8
Name=Dart editor
Comment=${pkgdesc}
Exec=/usr/bin/${pkgname}
Icon=/opt/${pkgname}/icon.xpm
Terminal=false
Type=Application
Categories=Development;
EOF

}

package() {

  msg2 "Install the assembly at /opt/${pkgname}..."
  install -dm755 "${pkgdir}"/opt/${pkgname}
  cp -a "${srcdir}"/dart/* "${pkgdir}"/opt/${pkgname}

  msg2 "Install links to the executables in /usr/bin..."
  install -dm755                   "${pkgdir}"/usr/bin
  ln -s /opt/${pkgname}/DartEditor "${pkgdir}"/usr/bin/${pkgname}

  msg2 "Install link to the config file in /etc..."
  install -dm755                       "${pkgdir}"/etc
  ln -s /opt/${pkgname}/DartEditor.ini "${pkgdir}"/etc/${pkgname}.ini

  msg2 "Install desktop application entry in /usr/share/applications..."
  install -Dm644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
