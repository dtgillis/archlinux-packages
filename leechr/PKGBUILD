# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=leechr
pkgver=0.4.8.7.2
pkgrel=1
pkgdesc="Automatic TV episode NZB leeching bot."
arch=('any')
url="http://code.google.com/p/leechr"
license=('GPL3')
depends=('python2')
makedepends=('sed')
changelog=Changelog
source=(http://leechr.googlecode.com/files/Leechr${pkgver}.zip)
md5sums=('8e5a72069c6e521d37b8d135a9ca1908')

package() {

  msg2 "Install the assembly at /opt/${pkgname}..."
  install -dm777           "${pkgdir}/opt/${pkgname}" # FIXME World writable because leechr needs its own directory to be writable at runtime...
  cp -r ${srcdir}/Leechr/* "${pkgdir}/opt/${pkgname}/"

  msg2 "Fix Python scripts shebang"
  sed -i -e 's/\#\!\/usr\/bin\/env\ python/\#\!\/usr\/bin\/env\ python2/' "${pkgdir}"/opt/leechr/{,tools/}*.py

  msg2 "Install links to the documentation resources at /usr/share/doc/${pkgname}..."
  install -dm755                         "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s /opt/${pkgname}/README_FIRST.txt "${pkgdir}/usr/share/doc/${pkgname}/README_FIRST.txt"
  ln -s /opt/${pkgname}/CHANGELOG.txt    "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG.txt"
  ln -s /opt/${pkgname}/demo.conf        "${pkgdir}/usr/share/doc/${pkgname}/demo.conf"
  ln -s /opt/${pkgname}/example.conf     "${pkgdir}/usr/share/doc/${pkgname}/example.conf"

  msg2 "Install links to copyright resources at /usr/share/licenses/${pkgname}..."
  install -dm755                "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /opt/${pkgname}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  msg2 "Install the example settings file at /etc/${pkgname}.conf..."
  cp "${pkgdir}"/opt/${pkgname}/{example,leechr}.conf
  install -dm755 "${pkgdir}/etc"
  ln -s "/opt/${pkgname}/leechr.conf" "${pkgdir}/etc/${pkgname}.conf"

  msg2 "Install an executable at /usr/bin/${pkgname}..."
  install -dm755 ${pkgdir}/usr/bin
  cat <<EOF > ${pkgdir}/usr/bin/${pkgname}
#!/bin/sh
/opt/${pkgname}/leechr.py "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:

