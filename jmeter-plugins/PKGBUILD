# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=jmeter-plugins
pkgver=0.4.2
pkgrel=2
pkgdesc="Custom set of plugins for Apache JMeter."
arch=(i686 x86_64)
url="http://code.google.com/p/jmeter-plugins"
license=('APACHE')
depends=('java-runtime>=6' 'jmeter>=2.4' 'bash')
changelog=Changelog
options=(!strip)
source=(http://${pkgname}.googlecode.com/files/JMeterPlugins-${pkgver}.zip)
md5sums=('59261639ba801216416676562b03c70d')

package() {

  # FIXME The serverAgent stuff is stripped off the package. 

  msg2 "Install assembly in /usr/share/java/${pkgname}..."
  install -dm755                                      "${pkgdir}/usr/share/java/${pkgname}"
  cp -r ${srcdir}/{LICENSE,README,*.jar,cmd,examples} "${pkgdir}/usr/share/java/${pkgname}"

  msg2 "Settings up links to libraries in /opt/jmeter/lib/ext..."
  install -dm755                                              "${pkgdir}/opt/jmeter/lib/ext"
  ln -s "/usr/share/java/${pkgname}/JMeterPlugins.jar"        "${pkgdir}/opt/jmeter/lib/ext/JMeterPlugins.jar"
  ln -s "/usr/share/java/${pkgname}/cmd/JMeterPluginsCMD.jar" "${pkgdir}/opt/jmeter/lib/ext/JMeterPluginsCMD.jar"

  msg2 "Install links to documentation resources in /usr/share/doc/${pkgname}..."
  install -dm755                            "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s /usr/share/java/${pkgname}/README   "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/java/${pkgname}/examples "${pkgdir}/usr/share/doc/${pkgname}/examples"

  msg2 "Install links to copyright resources at /usr/share/licenses/${pkgname}..."
  install -dm755                           "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/java/${pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"

  msg2 "Install executables in /usr/bin..."
  install -dm755 ${pkgdir}/usr/bin
  cat <<EOF > ${pkgdir}/usr/bin/${pkgname}
#!/bin/sh
"\$JAVA_HOME/bin/java" -jar '/opt/jmeter/lib/ext/JMeterPluginsCMD.jar' "\$@"
EOF

}

# vim:set ts=2 sw=2 et:
