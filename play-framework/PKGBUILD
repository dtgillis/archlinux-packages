# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=play-framework
pkgver=1.2.4
pkgrel=1
pkgdesc="Java/Scala web framework focused on developer productivity."
arch=(any)
url="http://www.playframework.org"
license=(APACHE)
depends=('java-environment>=5' 'python2')
optdepends=('scala: to use the Scala version of Play! framework'
            'bash-completion: bash auto-completion for the play command'
            'zsh-completions-git: zsh auto-completion for the play command')
install=${pkgname}.install
options=(!strip emptydirs)
changelog=Changelog
conflicts=(${pkgname}-git)
source=(http://download.playframework.org/releases/play-${pkgver}.zip)
md5sums=('ec8789f8cc02927ece536d102f5e649e')

build() {
  msg2 "Generate scripts for /etc/profile.d..."
  cat <<EOF > "${srcdir}"/${pkgname}.profile.d.sh
export PLAY_HOME=/opt/${pkgname}
EOF
  cat <<EOF > "${srcdir}"/${pkgname}.profile.d.csh
setenv PLAY_HOME "/opt/${pkgname}"
EOF

  msg2 "Generate executable for /usr/bin..."
  cat <<EOF > "${srcdir}"/${pkgname}.sh
#!/bin/sh
python2 \${PLAY_HOME}/play "\$@"
EOF

  msg2 "Strip unneeded stuff from distribution..."
  rm -rf "${srcdir}"/play-${pkgver}/python
}

package() {
  msg2 "Install the assembly at /opt/${pkgname}..."
  install -dm755                   "${pkgdir}"/opt/${pkgname}
  cp -r ${srcdir}/play-${pkgver}/* "${pkgdir}"/opt/${pkgname}

  msg2 "Set up the PLAY_HOME environment variable in /etc/profile.d..."
  install -Dm755 "${srcdir}"/${pkgname}.profile.d.sh  "${pkgdir}"/etc/profile.d/${pkgname}.sh
  install -Dm755 "${srcdir}"/${pkgname}.profile.d.csh "${pkgdir}"/etc/profile.d/${pkgname}.csh

  msg2 "Install an executable at /usr/bin/play..."
  install -Dm755 "${srcdir}"/${pkgname}.sh "${pkgdir}"/usr/bin/play

  msg2 "Install links to the documentation resources at /usr/share/doc/${pkgname}..."
  install -dm755                          "${pkgdir}"/usr/share/doc/${pkgname}
  ln -s /opt/${pkgname}/README.textile    "${pkgdir}"/usr/share/doc/${pkgname}/README.textile
  ln -s /opt/${pkgname}/documentation     "${pkgdir}"/usr/share/doc/${pkgname}/documentation
  ln -s /opt/${pkgname}/samples-and-tests "${pkgdir}"/usr/share/doc/${pkgname}/samples-and-tests

  msg2 "Install links to copyright resources at /usr/share/licenses/${pkgname}..."
  install -dm755                "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s /opt/${pkgname}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/

  msg2 "Install bash completion script..."
  install -dm755                                "${pkgdir}"/etc/bash_completion.d
  ln -s /opt/${pkgname}/support/play_completion "${pkgdir}"/etc/bash_completion.d/${pkgname}.sh
}

# vim:set ts=2 sw=2 et:
