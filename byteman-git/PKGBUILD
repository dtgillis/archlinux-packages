# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=byteman-git
pkgver=20111011
pkgrel=1
pkgdesc="Inject side effects into Java programs for the purpose of tracing and testing application behaviour."
arch=(i686 x86_64)
url="http://www.jboss.org/byteman"
license=('LGPL2.1')
depends=('java-environment>=6' 'bash')
makedepends=('git' 'maven' 'unzip')
changelog=Changelog
provides=('byteman')
conflicts=('byteman')
options=('!emptydirs')

_gitroot=git://github.com/bytemanproject/byteman.git
_gitname=byteman

build() {

  msg2 "Connecting to GIT server...."
  if [ -d "${srcdir}/${_gitname}" ] ; then
    ( cd "${srcdir}/${_gitname}" && git pull origin )
    msg2 "The local files are updated."
  else
    git clone $_gitroot "${srcdir}/${_gitname}"
  fi

  msg2 "Building..."
  mvn --file "${srcdir}/${_gitname}/pom.xml" \
      --batch-mode \
      --update-snapshots \
      clean install
}

package() {

  msg2 "Extracting assembly to /usr/share/java/${_gitname}..."
  install -dm755 ${pkgdir}/usr/share/java/${_gitname}
  unzip ${srcdir}/${_gitname}/download/target/${_gitname}-download-*-bin.zip -d ${pkgdir}/usr/share/java/${_gitname}
  mv ${pkgdir}/usr/share/java/${_gitname}/${_gitname}-download-*/* ${pkgdir}/usr/share/java/${_gitname}/

  msg2 "Setting permissions..."
  chmod -R 644 ${pkgdir}/usr/share/java/${_gitname}
  chmod 755 ${pkgdir}/usr/share/java/${_gitname}/bin/*

  msg2 "Set up the BYTEMAN_HOME environment variable in /etc/profile.d..."
  install -dm755 ${pkgdir}/etc/profile.d/
  cat <<EOF > ${pkgdir}/etc/profile.d/${_gitname}.sh
export BYTEMAN_HOME=/usr/share/java/${_gitname}
EOF
  cat <<EOF > ${pkgdir}/etc/profile.d/${_gitname}.csh
setenv BYTEMAN_HOME "/usr/share/java/${_gitname}"
EOF

  msg2 "Install links to the documentation resources at /usr/share/doc/${_gitname}..."
  install -dm755                          ${pkgdir}/usr/share/doc/${_gitname}
  ln -s /usr/share/java/${_gitname}/README ${pkgdir}/usr/share/doc/${_gitname}/README
  ln -s /usr/share/java/${_gitname}/docs   ${pkgdir}/usr/share/doc/${_gitname}/docs
  ln -s /usr/share/java/${_gitname}/sample ${pkgdir}/usr/share/doc/${_gitname}/sample

  msg2 "Install links to the executables in /usr/bin..."
  install -dm755                                    ${pkgdir}/usr/bin
  ln -s /usr/share/java/${_gitname}/bin/bmcheck.sh   ${pkgdir}/usr/bin/bmcheck
  ln -s /usr/share/java/${_gitname}/bin/bminstall.sh ${pkgdir}/usr/bin/bminstall
  ln -s /usr/share/java/${_gitname}/bin/bmjava.sh    ${pkgdir}/usr/bin/bmjava
  ln -s /usr/share/java/${_gitname}/bin/bmsubmit.sh  ${pkgdir}/usr/bin/bmsubmit
}

# vim:set ts=2 sw=2 et:
