# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=play-framework
pkgver=1.2.3
pkgrel=3
pkgdesc="Java/Scala web framework focused on developer productivity."
arch=(any)
url="http://www.playframework.org"
license=('APACHE')
depends=('java-environment>=5' 'python2')
optdepends=('scala: to use the Scala version of Play! framework'
            'bash-completion: bash auto-completion for the play command'
            'zsh-completions-git: zsh auto-completion for the play command')
install=play-framework.install
options=('!strip' 'emptydirs')
changelog=Changelog
conflicts=('play-framework-git')
source=(http://download.playframework.org/releases/play-${pkgver}.zip)
md5sums=('75822b1ec443239a4467147a94882442')

package() {

  msg2 "Install the assembly at /opt/${pkgname}..."
  install -dm755                   "${pkgdir}"/opt/${pkgname}
  cp -r ${srcdir}/play-${pkgver}/* "${pkgdir}"/opt/${pkgname}

  msg2 "Set up the PLAY_HOME environment variable in /etc/profile.d..."
  install -dm755 ${pkgdir}/etc/profile.d/
  cat <<EOF > "${pkgdir}"/etc/profile.d/${pkgname}.sh
export PLAY_HOME=/opt/${pkgname}
EOF
  cat <<EOF > "${pkgdir}"/etc/profile.d/${pkgname}.csh
setenv PLAY_HOME "/opt/${pkgname}"
EOF

  msg2 "Install an executable at /usr/bin/play..."
  install -dm755 ${pkgdir}/usr/bin
  cat <<EOF > "${pkgdir}"/usr/bin/play
#!/bin/sh
python2 \${PLAY_HOME}/play "\$@"
EOF
  chmod 755 "${pkgdir}"/usr/bin/play

  msg2 "Install links to the documentation resources at /usr/share/doc/${pkgname}..."
  install -dm755                          "${pkgdir}"/usr/share/doc/${pkgname}
  ln -s /opt/${pkgname}/README.textile    "${pkgdir}"/usr/share/doc/${pkgname}/README.textile
  ln -s /opt/${pkgname}/documentation     "${pkgdir}"/usr/share/doc/${pkgname}/documentation
  ln -s /opt/${pkgname}/samples-and-tests "${pkgdir}"/usr/share/doc/${pkgname}/samples-and-tests

  msg2 "Install links to copyright resources at /usr/share/licenses/${pkgname}..."
  install -dm755                "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s /opt/${pkgname}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/

  msg2 "Install bash completion script..."
  install -dm755                                "${pkgdir}"/etc/bash_completion.d
  ln -s /opt/${pkgname}/support/play_completion "${pkgdir}"/etc/bash_completion.d/${pkgname}.sh

}

# vim:set ts=2 sw=2 et:
