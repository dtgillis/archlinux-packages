# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
pkgname=byteman
pkgver=1.6.0
pkgrel=2
pkgdesc="Inject side effects into Java programs for the purpose of tracing and testing application behaviour."
arch=(i686 x86_64)
url="http://www.jboss.org/byteman"
license=('LGPL2.1')
depends=('java-environment>=6' 'bash')
changelog=Changelog
conflicts=('byteman-git')
source=(http://downloads.jboss.org/$pkgname/$pkgver/$pkgname-download-$pkgver-bin.zip)
md5sums=('c21d2ef28d9f0e6a4253eeb1f910440f')

package() {
  msg2 "Install the assembly at /usr/share/java/${pkgname}..."
  install -dm755                                  ${pkgdir}/usr/share/java/${pkgname}
  cp -r ${srcdir}/${pkgname}-download-${pkgver}/* ${pkgdir}/usr/share/java/$pkgname

  msg2 "Set up the BYTEMAN_HOME environment variable in /etc/profile.d..."
  install -dm755 ${pkgdir}/etc/profile.d/
  cat <<EOF > ${pkgdir}/etc/profile.d/${pkgname}.sh
export BYTEMAN_HOME=/usr/share/java/${pkgname}
EOF
  cat <<EOF > ${pkgdir}/etc/profile.d/${pkgname}.csh
setenv BYTEMAN_HOME "/usr/share/java/${pkgname}"
EOF

  msg2 "Install links to the documentation resources at /usr/share/doc/${pkgname}..."
  install -dm755                          ${pkgdir}/usr/share/doc/${pkgname}
  ln -s /usr/share/java/${pkgname}/README ${pkgdir}/usr/share/doc/${pkgname}/README
  ln -s /usr/share/java/${pkgname}/docs   ${pkgdir}/usr/share/doc/${pkgname}/docs
  ln -s /usr/share/java/${pkgname}/sample ${pkgdir}/usr/share/doc/${pkgname}/sample

  msg2 "Install links to the executables in /usr/bin..."
  install -dm755                                    ${pkgdir}/usr/bin
  ln -s /usr/share/java/${pkgname}/bin/bmcheck.sh   ${pkgdir}/usr/bin/bmcheck
  ln -s /usr/share/java/${pkgname}/bin/bminstall.sh ${pkgdir}/usr/bin/bminstall
  ln -s /usr/share/java/${pkgname}/bin/bmjava.sh    ${pkgdir}/usr/bin/bmjava
  ln -s /usr/share/java/${pkgname}/bin/bmsubmit.sh  ${pkgdir}/usr/bin/bmsubmit
}

# vim:set ts=2 sw=2 et:
