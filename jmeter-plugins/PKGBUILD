# Maintainer: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Source: https://github.com/nicoulaj/archlinux-packages
# TODO Add serverAgent stuff to the package.
pkgname=jmeter-plugins
_pkgname=JMeterPlugins
pkgver=0.5.3
pkgrel=1
pkgdesc="Custom set of plugins for Apache JMeter."
arch=(any)
url="http://code.google.com/p/jmeter-plugins"
license=(APACHE)
depends=('java-runtime>=6' 'jmeter>=2.4' 'bash')
optdepends=('zsh-completions: auto-completion for the jmeter-plugins command on Zsh')
changelog=Changelog
options=(!strip)
source=(http://${pkgname}.googlecode.com/files/${_pkgname}-${pkgver}.zip)
md5sums=('c7ab030628c80d144f937e161f81ed18')

build() {
  msg2 "Generate executable script for /usr/bin..."
  cat <<EOF > "${srcdir}/${pkgname}.sh"
#!/bin/sh
"\$JAVA_HOME/bin/java" -jar '/opt/jmeter/lib/ext/CMDRunner.jar' --tool Reporter "\$@"
EOF
}

package() {
  msg2 "Install libraries in /opt/jmeter/lib/ext..."
  install -Dm755 "${srcdir}/${_pkgname}.jar" "${pkgdir}/opt/jmeter/lib/ext/${_pkgname}.jar"
  install -Dm755 "${srcdir}/CMDRunner.jar"   "${pkgdir}/opt/jmeter/lib/ext/CMDRunner.jar"

  msg2 "Install documentation resources in /usr/share/doc/${pkgname}..."
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/doc/${pkgname}/README"
  cp -a "${srcdir}/examples"        "${pkgdir}/usr/share/doc/${pkgname}/"

  msg2 "Install copyright resources in /usr/share/licenses/${pkgname}..."
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  msg2 "Install executables in /usr/bin..."
  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
